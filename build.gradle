
buildscript {
  repositories {
    mavenCentral()
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'maven-publish'


repositories {
    maven { url "https://repo.maven.apache.org/maven2" }
}

configurations {
    jmhImplementation.extendsFrom testImplementation
}

sourceSets {
    jmh {
        java.srcDirs = ['src/jmh/java']
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

dependencies {
    compile 'log4j:log4j:1.2.17'
    testCompile 'junit:junit:4.6'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.36'
    jmhImplementation 'org.openjdk.jmh:jmh-core:1.36'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task testJar(type: Jar) {
    classifier = 'test'
    from sourceSets.test.output
}

task testSourcesJar(type: Jar) {
    classifier = 'test-sources'
    from sourceSets.test.allSource
}

task jmh(type: JavaExec, dependsOn: jmhClasses) {
  main = 'org.openjdk.jmh.Main'
  classpath = sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath
}

classes.finalizedBy(jmhClasses)

artifacts {
    archives javadocJar, sourcesJar, testJar, testSourcesJar
}

if (project.hasProperty("signing.enabled") && project.property("signing.enabled") == "true") {
  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
          name 'Tehuti'
          packaging 'jar'
          // optionally artifactId can be defined here
          description 'Tehuti is a simple metrics library providing statistical measurement, reporting and quota functionalities.'
          url 'http://tehuti.io'

          scm {
            connection 'scm:git:git@github.com:tehuti-io/tehuti.git'
            developerConnection 'scm:git:git@github.com:tehuti-io/tehuti.git'
            url 'https://github.com/tehuti-io/tehuti'
          }

          licenses {
            license {
              name 'The Apache License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          mailingLists {
            mailingList {
              name 'Tehuti mailing List'
              subscribe 'https://groups.google.com/forum/#!forum/tehuti'
              post 'tehuti@googlegroups.com'
              archive 'https://groups.google.com/forum/#!forum/tehuti'
            }
          }

          issueManagement{
            system 'GitHub'
            url 'https://github.com/tehuti-io/tehuti/issues'
          }

          developers {
            developer {
              id 'FelixGV'
              name 'Felix GV'
              email 'fgv@linkedin.com'
            }
          }
        }
      }
    }
  }

  signing {
    sign configurations.archives
  }

  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
      }
    }
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId project.group
      artifactId project.name
      version project.version

      println "######artifact details $groupId:$artifactId:$version"
      from components.java

      artifact sourcesJar
      artifact javadocJar
      artifact testJar
      artifact testSourcesJar

      pom {
        name = 'Tehuti'
        description = 'Tehuti is a simple metrics library providing statistical measurement, reporting and quota functionalities.'
        url = 'http://tehuti.io'

        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'FelixGV'
            name = 'Felix GV'
            email = 'fgv@linkedin.com'
            organization = 'LinkedIn'
            organizationUrl = 'linkedin.com'
          }
        }
        scm {
          connection = 'scm:git:git@github.com:tehuti-io/tehuti.git'
          developerConnection = 'scm:git:git@github.com:tehuti-io/tehuti.git'
          url = 'https://github.com/tehuti-io/tehuti'
        }
      }
    }
  }

  repositories {
    maven {
      name "LinkedInJfrog"
      url "https://linkedin.jfrog.io/artifactory/tehuti/"
      credentials {
        if (System.getenv('JFROG_USER') != null && System.getenv('JFROG_KEY') != null) {
          username System.getenv('JFROG_USER')
          password System.getenv('JFROG_KEY')
        }
      }
    }
  }
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

wrapper {
  gradleVersion = '6.5.1'
  distributionType = Wrapper.DistributionType.ALL
}

